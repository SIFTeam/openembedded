#@TYPE: Machine
#@NAME: Dreambox DM7020HD
#@DESCRIPTION: Machine configuration for the Dreambox DM7020HD

PREFERRED_VERSION_wpa-supplicant = "0.5.8"

#take care when you do changes on MACHINE_ESSENTIAL_EXTRA_RDEPENDS/RRECOMMENDS you have to increment the recipes/tasks/task-boot.bb PR

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "\
	kernel-module-nls-iso8859-1 \
	kernel-module-nls-iso8859-15 \
	kernel-module-nls-cp437 \
	kernel-module-nls-cp850 \
	kernel-module-nls-utf8 \
	v4l-dvb-module-dvb-core \
	v4l-dvb-module-stv0299 \
"

module_autoload_nls-iso8859-1 = "nls-iso8859-1"
module_autoload_nls-iso8859-15 = "nls-iso8859-15"
module_autoload_nls-cp437 = "nls-cp437"
module_autoload_nls-cp850 = "nls-cp850"
module_autoload_nls-utf8 = "nls-utf8"
module_autoload_dvb-core = "dvb-core"
module_autoload_stv0299 = "stv0299"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS = "\
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-ext2 \
	kernel-module-ntfs \
	kernel-module-iso9660 \
	kernel-module-udf \
	kernel-module-cdfs \
"

module_autoload_cifs = "cifs"
module_autoload_exportfs = "exportfs"
module_autoload_ext2 = "ext2"
module_autoload_xfs = "xfs"
module_autoload_iso9660 = "iso9660"
module_autoload_udf = "udf"
module_autoload_cdfs = "cdfs"
module_autoload_ntfs = "ntfs"

MACHINE_EXTRA_RRECOMMENDS = " \
	gst-plugin-dvbmediasink \
	dreambox-tpmd \
	dreambox-boot-progress \
	dreambox-enigma2-config \
	"

DVBMEDIASINK_CONFIG = "--with-dtsdownmix"

EXTRA_IMAGEDEPENDS += "dreambox-enigma2-config"

KERNEL_EXTRA_CMD = "--disable-compressor=lzo "

EXTRA_IMAGECMD_jffs2 = " -e 0x40000 -n -l"
IMAGE_CMD_jffs2 = "\
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS}/boot \
		--faketime \
		--disable-compressor=lzo \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${EXTRA_IMAGECMD}; \
	rm -rf ${IMAGE_ROOTFS}/boot/*; \
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS} \
		--faketime \
		${KERNEL_EXTRA_CMD} \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${EXTRA_IMAGECMD}; \
	sumtool \
		-i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		-o ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2.sum \
		${EXTRA_IMAGECMD}; \
	buildimage --brcmnand --arch ${MACHINE} -e 0x40000 -f 0x10000000 -s 4096 \
		-b 0x100000:${STAGING_LIBDIR}/dreambox-secondstage/main.bin.gz \
		-d 0x700000:${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		-d 0xF800000:${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2.sum \
		> ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.nfi"

MACHINE_FEATURES += "usbhost modem wifi"

# openpli specific features:
MACHINE_FEATURES += "32bpp hdtv switchoff lpcm dvb-c dvbapi5 tpm"

# wifiusblegacy enables legacy external usb wlan drivers
MACHINE_FEATURES += "wifiusblegacy"

CHIPSET = "bcm7405"

require conf/machine/include/dreambox-mipsel.inc
include conf/machine/include/autoload-usbserial.inc

SYSVINIT_ENABLED_GETTYS = ""
