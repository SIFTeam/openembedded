#@TYPE: Distribution
#@NAME: OpenPLi
#@DESCRIPTION: Embedded Linux Distribution Configuration for STB's

DISTRO_NAME ?= "openpli"
DISTRO_VERSION = "2.1"
DISTRO_TYPE ?= "release"

PLISVNURL ?= "svn://openpli.svn.sourceforge.net/svnroot/openpli"
PLISVNPROTO ?= "https"
PLISVNBRANCH ?= "trunk"

# Use bluetooth 4.0
DISTRO_BLUETOOTH_MANAGER = "bluez4"
# We want images supporting the following features (for task-base)
DISTRO_FEATURES = "nfs smbfs wifi madwifi nl80211 ext2 vfat ipv4 ipv6 usbhost alsa pci"
DISTRO_FEATURES += "largefile"
# Following features are for ARM and E500 based machines
DISTRO_FEATURES += "eabi"

# The following feature enables the MIPS ABI PLT extensions
# This feature needs GCC 4.4 binutils 2.19 or glibc/eglibc 2.9
# or higher

DISTRO_FEATURES += "mplt"

INHERIT += "debian"
INHERIT += "packaged-staging"
INHERIT += "package_ipk"

IMAGE_FSTYPES ?= "jffs2"
PREFERRED_PKG_FORMAT ?= "ipk"

IPKG_VARIANT = "opkg"

FEED_NAME ?= "${DISTRO_NAME}-${DISTRO_VERSION}"
DISTRO_FEED_URI ?= "http://downloads.pli-images.org/feeds/${FEED_NAME}/${MACHINE}"
DISTRO_FEED_PREFIX = "openpli"

DISTRO_DEV_MANAGER = "busybox-mdev"
IMAGE_DEV_MANAGER = "busybox-mdev"

PREFERRED_PROVIDER_hotplug = "busybox-mdev"
HOTPLUG = "busybox-mdev"

PREFERRED_PROVIDER_inetd = "busybox-inetd"

IMAGE_LOGIN_MANAGER = "busybox"

IMAGE_NAME = "${IMAGE_BASENAME}-${DISTRO_VERSION}-${MACHINE}"
CACHE ?= "${TMPDIR}/cache/${LIBC}/${MACHINE}"
DEPLOY_DIR ?= "${TMPDIR}/deploy/${LIBC}"
DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE}"

KERNEL = "kernel26"
MACHINE_KERNEL_VERSION = "2.6"

LIBC ?= "eglibc"
PREFERRED_GCC_VERSION ?= "4.4.4"

require conf/distro/include/sane-toolchain.inc

require conf/distro/include/openpli-unstable-versions.inc

PREFERRED_PROVIDER_opkg ?= "opkg"
PREFERRED_PROVIDER_virtual/update-alternatives ?= "opkg"

PREFERRED_PROVIDER_libfribidi = "libfribidi"
PREFERRED_VERSION_libsigc++-1.2 ?= "1.2.5"
# higher versions drag in mm-common, which drags in glib -> gtk+ -> x11
PREFERRED_VERSION_libsigc++-2.0 ?= "2.2.2"
PREFERRED_VERSION_swig-native ?= "1.3.39"
PREFERRED_VERSION_glib-2.0 ?= "2.26.1"
# avoid libdlna --> ffmpeg dependency
PREFERRED_VERSION_ushare ?= "1.1a"

PREFERRED_VERSION_samba ?= "3.5.6"

# amBX won't work with libusb-compat, needs the real thing
PREFERRED_PROVIDER_virtual/libusb0 = "libusb"

# distro-feed-configs: configuration files for the online feeds
# util-linux-ng-mount util-linux-ng-umount: busybox mount is broken
# angstrom-libc-fixup-hack: fixes an obscure bug with libc.so symlink
DISTRO_EXTRA_RDEPENDS += "\
  distro-feed-configs \
  3rd-party-feed-configs \
  angstrom-libc-fixup-hack \
"

# Additional content (can be masked with BAD_RECOMMENDATIONS)
DISTRO_EXTRA_RRECOMMENDS += " \
    kernel-module-af-packet \
"
